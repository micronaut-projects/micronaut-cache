buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.micronaut.build.internal:micronaut-gradle-plugins:4.0.0"
    }
}

subprojects { Project subproject ->
    repositories {
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }
    group "io.micronaut.cache"

    if (subproject.name != 'cache-bom') {
        apply plugin: "io.micronaut.build.internal.common"

        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java"
            if (subproject.name != 'cache-tck') {
                annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:${micronautDocsVersion}"
            }

            api "io.micronaut.reactor:micronaut-reactor"

            // TODO: Remove where micronaut-micrometer is upgraded to Micronaut 3
            api "javax.inject:javax.inject:1"

            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
            testImplementation("org.spockframework:spock-core:${spockVersion}") {
                exclude module: 'groovy-all'
            }
            testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
        }
    } else {
        apply plugin: "java-platform"
        version projectVersion
    }

    apply plugin: "io.micronaut.build.internal.dependency-updates"
    apply plugin: "io.micronaut.build.internal.publishing"
}

apply plugin: "io.micronaut.build.internal.docs"
apply plugin: "io.micronaut.build.internal.dependency-updates"
