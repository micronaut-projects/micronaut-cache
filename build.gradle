buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.1"
    }
}

subprojects { Project subproject ->

    group "io.micronaut.cache"

    apply plugin: "io.micronaut.build.common"

    micronautBuild {
        enforcedPlatform = true
    }

    if (subproject.parent.name == "examples" || subproject.name == "examples") {
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    apply plugin: "io.micronaut.build.publishing"

    dependencies {
        annotationProcessor "io.micronaut:micronaut-inject-java"
        if (subproject.name != 'cache-tck') {
            annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:${micronautDocsVersion}"
        }
        
        testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }
        testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
    }
    tasks.withType(Test) {
        useJUnitPlatform()
    }            
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

